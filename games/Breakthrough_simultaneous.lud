(define "ReachWinSimultaneous" 
    (if ((!= #2 (difference #2 (sites Occupied by:#1)))  (result #1 Win)))
)

(define "IsPlayerAt"
    (!= (who at:#2) #1)
)

(define "Move_Simultaneous" 
        (or {
            (move 
                (from (sites Occupied by:#1))
                (to (sites Direction from:(from) Forward distance:1) if:(is Empty (to)))
                #1
            )
            (move 
                (from (sites Occupied by:#1))
                (to (sites Direction from:(from) FL distance:1)
                    if:(and
                            (or 
                                (is Empty (to)) 
                                ("IsPlayerAt" #1 (to))
                            )
                            (not (= (to) (last To)))
                        )
                        (apply (remove (to)))
                )
                #1
            )
            (move 
                (from (sites Occupied by:#1))
                (to (sites Direction from:(from) FR distance:1)
                    if:(and
                            (or 
                                (is Empty (to)) 
                                ("IsPlayerAt" #1 (to))
                            )
                        (not (= (to) (last To)))
                        )
                        (apply (remove (to)))
                )
                #1
            )
        })
    
)

//------------------------------------------------------------------------------

(game "Breakthrough_simultaneous" 
    ("TwoPlayersNorthSouth") 
    (mode Simultaneous)
    (equipment { 
        (board (<Tiling:type> <Board:size>)) 
        (piece "Pawn" Each )
        (regions P1 (sites Top))
        (regions P2 (sites Bottom))
    }) 
    (rules 
        (start { 
            (place "Pawn1" (expand (sites Bottom)))
            (place "Pawn2" (expand (sites Top)))
        })
        (play 
            (or {
                ("Move_Simultaneous" P1)
                ("Move_Simultaneous" P2)
            })
        )

        
        (end {
            ("ReachWinSimultaneous" P1 (sites Top))
            ("ReachWinSimultaneous" P2 (sites Bottom))
        })
    )
)

//------------------------------------------------------------------------------

(option "Board" <Tiling> args:{ <type> }
    {
    (item "Square" <square> "The board is tiling by square.")* 
    (item "Hexagon" <hex> "The board is tiling by hexagon.") 
})

(option "Board Size" <Board> args:{ <size> }
    {
    (item "4x4" <4> "The game is played on a 4x4 board.") 
    (item "5x5" <5> "The game is played on a 5x5 board.") 
    (item "6x6" <6> "The game is played on a 6x6 board.") 
    (item "7x7" <7> "The game is played on a 7x7 board.") 
    (item "8x8" <8> "The game is played on a 8x8 board.")* 
    (item "9x9" <9> "The game is played on a 9x9 board.") 
    (item "10x10" <10> "The game is played on a 10x10 board.") 
    (item "11x11" <11> "The game is played on a 11x11 board.") 
    (item "12x12" <12> "The game is played on a 12x12 board.") 
})

//------------------------------------------------------------------------------

(metadata
    
    (info
        {
        (description "Invented in 2000 by Dan Troyka, Breakthrough is a Chess variant played only with pawns. The game here is a simultaneous version of Breakthrough")
        (rules "Played on an 8x8 board with a double contingent of chess pawns. Pieces move forward one orthogonally or diagonally. Pieces can capture by moving diagonally. The first player to reach the opponent's edge of the board wins. A player also wins by default if they capture all of the opponent's pieces.")
        (source "<a href=\"https://en.wikipedia.org/wiki/Breakthrough_(board_game)\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
        (id "73")
        (version "1.0.0")
        (classification "board/race/reach")
        (publisher "LIP6")
        (credit "Aymeric Behaegel")
        (date "2023")
        }
    )
    
    (graphics {
        (board Style Chess)
    })
    
    (ai 
        "Breakthrough_ai"
    )
    
)